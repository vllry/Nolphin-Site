@name Nolphin V4 Engine
@inputs W A S D Up Down OnOff ForceOff ForceOn X Y Z Board:entity
@outputs ID On 
@outputs 
@persist Roll Yaw Pitch P2 R2 Engines ForceOn
@persist ZThrust DiffZ InternalZ ZForce AddedZ Z2 For2
@persist YForce YAdjust LocalX LocalY
#Nebcorp automatic hovering gyrations and maneuverabilities calculation and excecution chip
##Version 4.0##
#Most recent time I adjusted the date: 
##Feb 8th, 11:30##
 

###<< Configuration >>

#Yaw (turning) Mode:
#1: "Friction based", for more realistic turning (if you are spun, the ship stabalizes at current Yaw)
#2: "Fixed Yaw", ideal for more controlled turning (if you are spun, the ship returns to your desired Yaw)
YawMode = 1

RollLeaning = 1  #0 disables (Left/right leaning)
PitchLeaning = 1  #0 disables (Forward/backward leaning)
SmartZ = 1 #0 behaves like hoverballs, snapping back to set Z. 1 resets targetZ if its too distant (recommended)
 
RSpeed = 2  #0.2 - 10, The primary speed control multiplier for forward/back motions. Default 1
TSpeed = 1  #0.2 - 4, The turning speed control multiplier. Default 1
ZAcceleration = 1  #0.2 - 4, The speed of acceleration for going up/down. Default 1
ZTopSpeed = 1  #0.2 - 3, The top speed going up/down. Default 1
 
Friction = 5  #1 - 60, higher reduces effect. Controls how quickly the ship comes to a stop. Default 5

#Troubleshooting:
RadiusModifier = 1 #0.2 - 2. Normally, higher is more accurate. If stability is compromised OR for thin ships, lower this. 
YawInvert = 0 #0 off, 1 on. If the ship keeps irrationally spinning, try switching this.
 
#Things you probably should never ever modify upon pain of death
RagdollMode = 0 #KEEP OFF
 
###<< End of Configuration >>
 
#NOTES ON INPUTS:
#Board:entity is wired to a source of entitydata about your ship, such as an Entity Marker
#WASD/Up/Down: Does not need to be from an APC. Only send values of 1 or 0 for each. 
#OnOff toggles the internal engine; Force simply sets the engine.
#Z overrides the height controls and simply hovers at the inputted height.
#To use XYZ-tracking mode, wire up your desired location to the XYZ inputs, and the Nolphin will swiftly fly there.
 
 
 
 
 
 
#Declares common variables
On = Engines
if(On) {interval(20)} else{interval(1000)} #Slows down excecution when off
ID = Board:id()
Pitch = Board:angles():pitch()
Yaw = Board:angles():yaw()
Roll = Board:angles():roll()
TarR=(($Roll)*8+Roll) + (R2 *0.45 *RollLeaning)
TarP=(($Pitch)*8+Pitch) - (P2 *PitchLeaning)
GPSMode = (X & Y & Z) #When to activate GPSMode
 
#Power settings
if(~OnOff & OnOff) {Engines = !Engines}
if(ForceOff) {Engines = 0} 
if(ForceOn) {Engines = 1} elseif(~ForceOn & !ForceOn) {Engines = 0}

#Forward Thrust, smoothened
For1 = (W-S) *100
if(For2 > For1) {if(For1 == 0 | (For1 == -100 & (For2 > 0))) {For2 -= 2.1} else{For2 -= 0.70}}
elseif(For1 > For2) {if(For1 == 0 | (For1 == 100 & (For2 < 0))) {For2 += 2.1} else{For2 += 0.70}}
if(For2 < 1 & For2 > 0) {Forward = 0} else{Forward = For2 / 333}

#Additional Z to add, smoothened
Z3 = ZTopSpeed *100
Z1=(Up-Down)* Z3
if(Z2 > Z1) {if(Z1 == 0 | (Z1 == -Z3 & (Z2 > 0))) {Z2 -= 0.84 * ZAcceleration} else{Z2 -= 0.28 * ZAcceleration}}
elseif(Z1 > Z2) {if(Z1 == 0 | (Z1 == Z3 & (Z2 < 0))) {Z2 += 0.84 * ZAcceleration} else{Z2 += 0.28 * ZAcceleration}}
if(Z2 < 1 & Z2 > 0) {ZForce = 0} else{ZForce = Z2 / 5}
AddedZ += ZForce

#Additional Pitch, smoothened
if(ZForce > 15) {P2 = 45} elseif(ZForce < -15) {P2 = -45} else{P2 = ZForce * 3}

#Additional Roll, smoothened
R1 = (D - A) * 100
if((R2 < R1) & (R2 > R1 + 1)) {R2 = 0} #Filters out petty decimals like 0.31
elseif(R2 > R1) {if(R1==0 | (R1 == -100 & (R2 > 0))) {R2 -= 0.9} else{R2 -= 0.3}} 
elseif(R1 > R2) {if(R1==0 | (R1 == 100 & (R2 < 0))) {R2 += 0.9} else{R2 += 0.3}}

#Z Control
if(InternalZ == 0) {InternalZ = Board:pos():z() + 35}
if(Z != 0) { #If we're recieving a target Z to go to, go.
DiffZ = Z - Board:pos():z()
ZThrust = -(DiffZ + $DiffZ * 15)  
} else { #Otherwise, default to current position.
DiffZ = InternalZ - Board:pos():z() + AddedZ
if(SmartZ)   { #Config switch
if(DiffZ > 28) {AddedZ -= (DiffZ - 28)} #Resets if we're waaay below target.
elseif(DiffZ < -28) {AddedZ -= (DiffZ + 28)}   } #Resets if we're waaay above target.
ZThrust = -(DiffZ + $DiffZ * 5)}

#XY OR Friction, Rear/Right Maneouverability Thrusters
if(GPSMode == 0) { #Friction mode
XFriction = Board:velL():x() / Friction
if(XFriction < 0 & !W) {XSlow = XFriction} elseif(XFriction > 0 & !S) {XSlow = XFriction} else{XSlow = 0} 
Rear = 40 * Forward * RSpeed + XSlow #- RearZThrust
Right = (Board:velL():y() / Friction)# + RightZThrust2  
}
elseif(GPSMode == 1) { #XY tracking Mode
SubtractedV = vec(X - Board:pos():x(), Y - Board:pos():y(), Z - Board:pos():z())
LocalX = SubtractedV:dot(Board:forward())
LocalY = SubtractedV:dot(Board:right())
Rear =  (  (-($LocalX*4 + LocalX) / 7.5) + (Board:velL():x() / Friction)  ) / 2
Right = (  (($LocalY*4 + LocalY) / 7.5) + (Board:velL():y() /Friction)  ) /2  
}

#Yaw, FLSide thruster
if(YawMode == 1) { #Friction based
FLSide = (D - A) * (1 + For2 / 60) + ((!A & !D) * Board:angVel():yaw() / Friction) }
elseif(YawMode == 2) { #Forced Yaw based
if(!Engines) {YForce = -Yaw}
YForce += (D - A) * 1.4 * TSpeed
if(YForce < -179) {YForce = 179}
if(YForce > 179) {YForce = -179}
YAdjust = angnorm(Yaw + YForce)
FLSide = (($YAdjust) * 10 + YAdjust) *1.5 *TSpeed}
else{FLSide = (D - A) * TSpeed} #Default to basic

#Turns it off + resets
if(!Engines) {AddedZ = 0, InternalZ = Board:pos():z()+35, Z2 = 0, For2 = 0
North = 0, East = 0, South = 0, West = 0, Rear = 0, Right = 0, FLSide = 0, ZThrust = 0}


#Internal Thruster Variables
Weight = Board:mass() * 0.8
Radius = Board:radius() * RadiusModifier
CMass = Board:massCenterL()
UpZ = Board:up():z() + 0.1
if(RagdollMode) {Radius = 100, Weight = 100} #Lawl
if(YawInvert) {YI = -1} else{YI = 1} #Negates the yaw thruster, see config

#Applies the actual thrust
if(Engines) {
Board:applyForce(vec(0,0,-ZThrust) * Weight*4) #Primary Z

Board:applyOffsetForce(vec(0,0,TarP) * Weight/2, Board:toWorld(vec(Radius,0,0)) *UpZ + CMass) #North
Board:applyOffsetForce(vec(0,0,-TarP) * Weight/2, Board:toWorld(vec(-Radius,0,0)) *UpZ + CMass) #South
Board:applyOffsetForce(vec(0,0,TarR) * Weight/2, Board:toWorld(vec(0,-Radius,0)) *UpZ + CMass) #West
Board:applyOffsetForce(vec(0,0,-TarR) * Weight/2, Board:toWorld(vec(0,Radius,0)) *UpZ + CMass) #East

Board:applyForce(Board:forward() * -Rear * Weight/2) #Rear, handles movement and friction
Board:applyForce(Board:right() * Right * Weight/2) #Right, handles friction

Board:applyOffsetForce(Board:right() *vec(FLSide,FLSide,0) *YI *Weight/4, Board:toWorld(vec(Radius,-Radius,0)) + CMass) #Yaw thruster
}
